apply plugin: 'java'

project.ext.tags = ['bitbucket-cloud']
group = 'se.bjurr.bitbucketcloud'
description = 'Rest client for Bitbucket CLoud'

buildscript {
 repositories {
  mavenLocal()
  mavenCentral()
  maven { url "https://jitpack.io" }
 }
 dependencies {
  classpath 'com.github.tomasbjerre:gradle-scripts:master-SNAPSHOT'
  classpath 'org.openapitools:openapi-generator-gradle-plugin:3.3.4'
 }
}
apply from: project.buildscript.classLoader.getResource('java.gradle').toURI()
apply from: project.buildscript.classLoader.getResource('changelog.gradle').toURI()
apply from: project.buildscript.classLoader.getResource('release.gradle').toURI()
apply plugin: 'org.openapi.generator'

dependencies {
 compile 'io.swagger:swagger-annotations:1.5.3'
 compile 'javax.ws.rs:javax.ws.rs-api:2.1.1'
 compile 'com.fasterxml.jackson.datatype:jackson-datatype-joda:2.9.9'
 compile 'com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.9.9'
 compile 'javax.validation:validation-api:2.0.1.Final'
}

sourceSets {
 main {
  java {
   srcDir 'src/main/java'
   srcDir 'src/gen/java'
  }
 }
}

openApiGenerate {
 generatorName = "jaxrs-spec"
 skipOverwrite = false
 inputSpec = "$rootDir/swagger.yml".toString()
 validateSpec = false
 outputDir = "$rootDir".toString()
 apiPackage = "se.bjurr.bitbucketcloud.gen.api"
 modelPackage = "se.bjurr.bitbucketcloud.gen.model"
 systemProperties = [
  interfaceOnly: "true",
  generatePom: "false",
  useJackson: "true"
 ]
}

compileJava.dependsOn 'openApiGenerate'

googleJavaFormat {
  include '**/*.java'
  exclude 'src/gen/**/*.java'
}
